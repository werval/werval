buildscript {
    repositories {
        maven { url "$rootProject.projectDir.absolutePath/../repository" }
        mavenCentral();
        maven { url "http://repo.gradle.org/gradle/libs-releases-local" }
    }
    dependencies {
        classpath "org.qiweb:org.qiweb.gradle:0"
    }
}

dependencies {

    compile "org.qiweb:org.qiweb.api:$version"
    compile "org.qiweb:org.qiweb.lib:$version"
    compile libs.jackson_core

    runtime "org.qiweb:org.qiweb.server.bootstrap:$version"
    runtime libs.logback
    runtime webjars.bootstrap
    runtime webjars.jquery

    testCompile "org.qiweb:org.qiweb.test:$version"
    testCompile libs.rest_assured
    testCompile libs.fluentlenium

}

apply plugin: 'qiweb'
apply plugin: 'application'

applicationName = "url-shortener"
mainClassName = "org.qiweb.server.bootstrap.Main"
applicationDistribution.from( "src/main/resources" ) {
    include "*.conf"
    into "etc"
}
tasks.startScripts {
    doLast {
        def scriptShellFile = file "${outputDir}/${applicationName}"
        def scriptBatchFile = file "${outputDir}/${applicationName}.bat"
        scriptShellFile.text = scriptShellFile.text.replace( 'CLASSPATH=$APP_HOME/lib',
                                                             'CLASSPATH=$APP_HOME/etc/:$APP_HOME/lib')
        scriptBatchFile.text = scriptBatchFile.text.replace( 'CLASSPATH=%APP_HOME%\\lib',
                                                             'CLASSPATH=%APP_HOME%\\etc\\;%APP_HOME%\\lib' )
    }
}
artifacts {
    archives distZip
}
