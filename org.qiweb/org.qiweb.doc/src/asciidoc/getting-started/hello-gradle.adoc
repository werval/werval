
=== Hello Gradle QiWeb

> Automation evolved

In this short tutorial we'll enhance an application generated by the `qiweb` command line using Gradle.

IMPORTANT: You need to install http://gradle.org[Gradle] to be able to complete this tutorial.

Create a `build.gradle` file at the application root with the following content:

.`./build.gradle`
["source","groovy",subs="attributes,callouts"]
----
buildscript {	
	repositories {
    	maven {
    		url "https://repo.codeartisans.org/qiweb"
    		credentials { username = "qiweb"; password = "qiweb" } // <1>
		}
	}
	dependencies { classpath 'org.qiweb:org.qiweb.gradle:{qiweb-version}' }      // <2>
}
repositories {
    maven {
    	url "https://repo.codeartisans.org/qiweb"
    	credentials { username = "qiweb"; password = "qiweb" }     // <3>
	}
}

apply plugin: 'java'        // <4>
apply plugin: 'qiweb'       // <5>
apply plugin: 'application' // <6>

mainClassName = 'org.qiweb.server.bootstrap.Main' // <7>
applicationName = 'hello-qiweb'                   // <8>

dependencies {

    compile "org.qiweb:org.qiweb.api:{qiweb-version}" // <9>
    // Add application compile dependencies here

    runtime "org.qiweb:org.qiweb.server.bootstrap:{qiweb-version}" // <10>
    // Add application runtime dependencies here

    testCompile "org.qiweb:org.qiweb.test:{qiweb-version}" // <11>
    // Add application test dependencies here

}

----
<1> Declare QiWeb repository for the build
<2> Add QiWeb Gradle Plugin to build classpath
<3> Declare QiWeb repository for the application
<4> Apply the Gradle http://www.gradle.org/docs/current/userguide/java_plugin.html[Java Plugin]
<5> Apply the QiWeb Gradle plugin
<6> Apply the Gradle http://gradle.org/docs/current/userguide/application_plugin.html[Application Plugin] that provide scripts and packaging
<7> Set the application main class to `org.qiweb.server.bootstrap.Main` for production mode
<8> Name the application archive and startup scripts
<9> Add compile scope dependency to QiWeb API
<10> Add runtime scope dependency to QiWeb Netty Server Bootstrap
<11> Add test scope dependency to QiWeb Test Support


And that's it!
You can now use Gradle to compile, develop, test, assemble and run your application.

CAUTION: If you did'nt use the `qiweb` command line to generate an application skeleton you miss an application secret.
You can generate a new one by typing `gradle -q secret` on the command line.
Don't forget to add it to your `application.conf` file with the `app.secret` key.

.Run in development mode
[source]
----
user@host ~/hello-qiweb $ gradle devshell
----

.Run tests
[source]
----
user@host ~/hello-qiweb $ gradle check
----

.Run in production mode
[source]
----
user@host ~/hello-qiweb $ gradle run
----

.Generate ZIP distribution
[source]
----
user@host ~/hello-qiweb $ gradle distZip
----

TIP: If you need RPM or DEB packaging, use the https://github.com/nebula-plugins/gradle-ospackage-plugin[gradle-ospackage-plugin]
