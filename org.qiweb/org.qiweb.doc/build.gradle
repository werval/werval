// QiWeb Documentation

def htmlTarget = 'build/classes/main/org/qiweb/doc/html'

// Static assets
task copyAssets( type: Copy ) {
    from 'src/assets'
    into htmlTarget
}


// Asciidoc processing
buildscript {
    repositories {
        mavenRepo name: 'Bintray Asciidoctor repo', url: 'http://dl.bintray.com/content/aalmiray/asciidoctor'
        mavenRepo name: 'Bintray JCenter', url: 'http://jcenter.bintray.com'
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:0.5.0'
    }
}
apply plugin: 'asciidoctor'
asciidoctor {
    sourceDir = file( "src/asciidoc" )
    outputDir = file( htmlTarget )
    backend = 'html5'
    options = [
        compact: true,
        attributes: [
            'qiweb-version': version,
            'toc2': '',
            'stylesheet': 'stylesheets/foundation.css',
            'docinfo1': '',
        ]
    ]
    logDocuments = true
}
asciidoctor.dependsOn copyAssets
jar.dependsOn asciidoctor


// Embedded Javadoc
configurations { umlgraphDoclet }
dependencies { umlgraphDoclet 'org.umlgraph:umlgraph:5.6.6' }
def embeddedJavadocProjects() {
    rootProject.subprojects.findAll { p -> p.name == 'org.qiweb.api' || p.name == 'org.qiweb.stdlib' }
}
task embeddedJavadoc( type: Javadoc ) {
    title = "QiWeb API Reference (${version})"
    source = embeddedJavadocProjects().collect { p -> p.sourceSets.main.allJava }
    classpath = files( embeddedJavadocProjects().collect { p -> p.sourceSets.main.compileClasspath } )
    destinationDir = file( "${htmlTarget}/api" )
    options.encoding = 'UTF-8'
    options.overview = file( 'src/javadoc/overview.html' )
    options.docFilesSubDirs = true
    options.links( "http://docs.oracle.com/javase/7/docs/api/", "http://www.slf4j.org/apidocs/" )
    options.group( [ "QiWeb API": [ "org.qiweb.api", "org.qiweb.api.*" ], "QiWeb Standard Library": [ "org.qiweb.controller" ] ] )
    options.docletpath = configurations.umlgraphDoclet.files.asType( List )
    options.doclet = "org.umlgraph.doclet.UmlGraphDoc"
}
embeddedJavadoc.dependsOn copyAssets
jar.dependsOn embeddedJavadoc
